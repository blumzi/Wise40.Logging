#!/bin/gawk -f

function format_start_suspect(reason) {
    print "## Start suspect (" reason ")"
}

function format_end_suspect() {
    print "## End suspect"
}

# 15:48:19.359 
function timestamp2millis(t, w) {
    split(t, w, /[:\.]/)
    return (((w[1] * 3600 * 1000)) + ((w[2] * 60) * 1000) + (w[3] * 1000) + w[4]) + 0.0
}

function time_interval(t, ret, h, m, s, ms) {
    ret = ""
    ms = t % 1000
    t = (t - ms) / 1000
    s = t % 60
    t = (t - s) / 60
    m = t % 60
    t = (t - m) / 60
    h = t % 60

    if (h != 0)
        return sprintf("%d:%02d:%02d.%03d", h, m, s, ms)
    if (m != 0)
        return sprintf("%d:%02d.%03d", m, s, ms)
    if (s != 0)
        return sprintf("%d.%03d", s, ms)
    return sprintf("%dms", ms)
}

function print_in_progress(s, line, a, active) {
    line = substr(s, 1, 52)
    active = 0
    for (a = 0; a < length(actions); a++) {
        if (in_progress(actions[a]))
            active = 1
        if (active && in_progress(actions[a]))
            line = line "|  "
    }
    line = line substr(s, 53)
    print line
}

function print_start_progress(action, s, a, line, active) {
    active = 0
    in_progress_dict[action] = 1
    line = substr(s, 1, 52)
    for (a = 0; a < length(actions); a++) {
        if (in_progress(actions[a]) && !active) {
            #print "print_start_progress: first active: " actions[a]
            active = 1
        }
        if (active == 1 && in_progress(actions[a])) {
            #print "print_start_progress: checking for action: " action ": actions[" a "]: "  actions[a]
            if (actions[a] == action)
                line = line "+- "
            else
                line = line "|  "
        }
    }
    line = line substr(s, 53)
    print line
}

function print_end_progress(action, s, a, line, active) {
    line = substr(s, 1, 52)
    active = 0
    for (a = 0; a < length(actions); a++) {
        if (in_progress(actions[a]))
            active = 1
        if (active && in_progress(actions[a])) {
            if (actions[a] == action)
                line = line "+- "
            else
                line = line "|  "
        }
    }
    line = line substr(s, 53)
    in_progress_dict[action] = 0
    print line
}

function in_progress(action) {
    return in_progress_dict[action]
}

function clear_progress() {
    for (a = 0; a < length(actions); a++) 
        in_progress_dict[actions[a]] = 0
}
    

BEGIN {
    slew_number = 0
    #unsafe_reasons = ""
    rate = ""
    prev_slewing_reason = ""
    delete in_progress_dict
    actions[0] = "shutdown"
    actions[1] = "shutter"
    actions[2] = "parking"
    actions[3] = "slewing"

    for (i = 0; i < length(actions) ; i++) {
        # print "action[" i "]:  " actions[i]
        in_progress_dict[actions[i]] = 0
    }
    changed_to_unsafe = 0
    showed_reasons = 0
}

{
    sub("\r", "")
}

/^##/ {
    print
    next
}

/======/ {
    print "\n" $0 "\n"
    clear_progress()
    next
}

/Waiting for dome to arrive to/ { next }

/DebugASCOM[[:space:]]*SlewToTargetAsync/ {
    slew_number++
    slew_start = timestamp2millis($1)
    print "\n"
    print "##############"
    print "# Slew " slew_number
    print "##############"
    print
    next
}

/Tracking Set/ { print_in_progress($0); next }
/DomeSlaveDriver:SlewToAz/ { print_in_progress($0); next }
/Slewer Done at/ { print_in_progress($0); next }
/DebugFocuser     Move.*from:/ {
    print_in_progress($0)
    next
}

/DebugDome.*Too short:/ {
    print_in_progress($0)
    next
}

/started.*the shutter/ {
    print_start_progress("shutter", $0)
    shutter_range_index = 1
    next
}

/DebugShutter     Stop: stopped/ {
    print_end_progress("shutter", $0)
    delete shutter_ranges
    next
}

in_progress("shutter") && /ShutterRange: returning:/ {
    range = $NF
    if (range in shutter_ranges) {
        ;
    } else {
        shutter_ranges[range] = 1
        print_in_progress($0)
    }
    next
}


/axis.*has stopped moving/ {
    print_in_progress($0)
    next
}

# 13:38:46.499 UT 15456,30,125       DebugLogic       _doSlewToCoordinatesAsync: Slewer "Ra" completed with status: RanToCompletion
/Slewer.*completed with status/ {
    slewer = match($7, ".Ra.") ? "axisPrimarySlewer" : "axisSecondarySlewer"
    sub("DoSlewToCoordinatesAsync:.*completed", slewer ": completed")
    print_in_progress($0)
    next
}

/DebugASCOM.*AbortSlew/ {
    print_in_progress($0)
    next
}

/[Aa]borted/ {
    print_in_progress($0)
    next
}

/DoShutdown\(/ {
    if (match($0, "waiting"))
        next

    gsub("DoShutdown", "Shutdown")
    if (match($0, "starting activity")) {
        print ""
        print_start_progress("shutdown", $0)
    } else if (match($0, "ending activity")) {
        print_end_progress("shutdown", $0)
        print ""
    } else
        print_in_progress($0)
    next
}

/Park: started/ {
    print_start_progress("parking", $0)
    next
}

/Park: all done/ {
    print_end_progress("parking", $0)
    next
}

/Slew cancelled/ {
    print_in_progress($0)
    next
}

/closeEnoughRates:/ {
    print_in_progress($0)
    next
}

# 15:48:24.352 UT 19352,18,-1        DebugSafety      UnsafeReasons: Sun - The Sun elevation (5.4 deg) is higher than 5.0 deg;Wind - not ready (only 0 of 3 readings);Clouds - not ready (only 0 of 3 readings);Rain - not ready (only 0 of 2 readings);Humidity - not ready (only 0 of 4 readings)
/UnsafeReasons:/ {
    if (changed_to_unsafe && !showed_reasons && NF > 5) {
        pre = substr($0, 1, 67)
        sub("Unsafe", "  Unsafe", pre)
        n = split(substr($0, 68), arr, ";")
        for (i = 1; i < n; i++)
            current_reasons[i-1] = arr[i]
        nreasons = length(current_reasons)
        if (! 0 in unsafe_reasons)
            unsafe_nreasons = 0
        same = (unsafe_nreasons == nreasons)

        if (same) {
            # same number, check if same elements
            for (i = 0; i < nreasons; i++)
                if (unsafe_reasons[i] != current_reasons[i]) {
                    same = 0
                    break
                }
        }

        if (! same) {
            delete unsafe_reasons
            i = 0
            for (i = 0; i < nreasons; i++) {
                unsafe_reasons[i] = current_reasons[i]
                print_in_progress(pre unsafe_reasons[i])
            }
        }
        showed_reasons = 0
        changed_to_unsafe = 0
    }
    next
}

/DebugSafety.*changed from/ {
    changed_to_unsafe = ($NF == "Unsafe")
    #print ">>> ==" $0 "==, changed_to_unsafe: " changed_to_unsafe

    print_in_progress($0)
    next
}

/all slewers done/ {
    print_in_progress($0 " duration: " time_interval(timestamp2millis($1) - slew_start))
    next
}

# 15:03:18.878 UT 16244,26,63        DebugLogic       (64277042) mp[axisPrimary, rateGuide, EastMotor,Tracking].velocity: 0.0000000452 rad/ms, millis: 63723866598878, dx: 0.0000045211, dt: 100.0000000000, True
/mp\[axis/ {
    sub(",", "", $7)
    rate = $7
    sub("mp.axis", "axis", $6)
    sub(",", "", $6)
    axis = $6
    next
}

# 15:08:35.981 UT 5084,18,158        DebugLogic       mp[EastMotor,Tracking].lowestRad: 0.0000020592, highestRad: 0.0000791239, rate: rateGuide
#/mp\[.*lowestRad/ {
#    r = rate
#    lowest = sprintf("%.8f", $6)
#    highest = sprintf("%.8f", $8)
#    if (match($0, /Tracking/))
#        r = r "+Tracking"
#    sub(/mp\[.*/, "")
#    print_in_progress($0 "mp[" axis ", " r "]: lowestRad: " lowest " highestRad: " highest)
#    next
#}

/Slewing Get -/ {
    was_slewing = in_progress("slewing")
    slewing_now = ($8 == "True") ? 1 : 0;
    if (slewing_now) {
        match($0, /[(][^)]*[)]/)
        slewing_reason = substr($0, RSTART + 1, RLENGTH - 2)
        if (slewing_reason != prev_slewing_reason) {
            prev_slewing_reason = slewing_reason
        }
    }
    sub(/Get.*/, "")
    if (slewing_now != was_slewing) {
        if (was_slewing && !slewing_now) {
            print_end_progress("slewing", $0 "DONE. duration: " time_interval(timestamp2millis($1) - slewing_start))
        } else if (!was_slewing && slewing_now) {
            print_start_progress("slewing", $0 "STARTED - " slewing_reason)
        }

        if (slewing_now && !was_slewing)
            slewing_start = timestamp2millis($1)
    }
    next
}

/DebugLogic[[:space:]]*Throw:/ {
    sub("Throw", "Cancellation")
    print_in_progress($0)
    next
}

/PulseGuide: Direction=/ {
    print_in_progress($0)
    next
}

# 17:10:02.343 UT 21100,17,2633      DebugDome        WiseDome: SlewToAzimuth(23.3°, reason: Follow telescope to new target) => at: 90.5°, dist: 67.2°), moving Decreasing
/WiseDome: SlewToAzimuth\(/ {
    target = $6
    sub(/.*\(/, "", target)
    sub(/,.*/, "", target)

    for (i = 1 ; i <= NF; i++) {
        if ($i == "reason:") {
            reason = ""
            for (j = i + 1; j <= NF && ! match($j, ".*)"); j++)
                reason = reason $j " "
            sub(")", "", $j)
            reason = reason $j
            break
        }
        sub(")", "", reason)
    }
    
    dome_slew[target,"start"] = $1
    dome_slew[target,"reason"] = reason
    next
}
    
# 17:10:34.414 UT 21100,23,-1        DebugDome        WiseDome:Stop(Reached target) Fully stopped at az: 22.6°, target: 23.3° encoder: 408,  after 2 tries
/WiseDome:Stop.*Fully stopped/ {
    for (i = 1; i <= NF; i++)
        if ($i == "target:") {
            target = $(i+1)
            break
        }
    dome_slew[target,"end"] = $1
    dome_slew[target,"millis"] = timestamp2millis(dome_slew[target,"end"]) - timestamp2millis(dome_slew[target,"start"])

    too_long_minutes = 5
    too_long_millis = too_long_minutes * 60 * 1000

    file = "dome_issues.txt"
    message = "start: " dome_slew[target,"start"] " UT, end: " dome_slew[target,"end"] " UT, duration: " (dome_slew[target,"millis"] / 1000) " seconds, reason: " dome_slew[target,"reason"]
    if (dome_slew[target,"millis"] > too_long_millis) {
        print message ", Too long!" >> file
        fflush(file)
    } else if (match($0, "AbortSlew")) {
        print message ", Aborted by AbortSlew!" >> file
        fflush(file)
    }
}

/Backoff/ {
    print_in_progress($0)
    next
}

/DebugSafety.*(Started|Ended) bypass/ {
    reason =  match($0, "temporary") ? "(internal) for Shutdown" : "by Operator\n"
    action = match($0, "Started") ? "Started" : "Ended"
    line = "Safety bypass: " action " " reason

    print ""
    sub("DebugSafety.*", "DebugSafety      " line, $0) 
    print_in_progress($0)
}

#egrep '(axis.*has stopped moving|Slewer.*completed with status|========|AbortSlew|Slew cancelled|closeEnoughRates:|doShutdown:|[Aa]borted|SlewToTargetAsync|Tracking Set|DomeSlaveDriver:SlewToAz|Slewer Done at|started.*the shutter|DebugShutter     Stop: stopped| PulseGuide: Direction|DebugFocuser.*(Move:|stopped.*state: None))' | egrep -v '(Waiting for dome to arrive to|doShutdown: waiting)' | sed 
#    -e 's;^\(.*SlewToTargetAsync.*\)$;\n\1;' 
#    -e 's;^\(.*started.*the shutter.*\)$;\n\1\n;' 
#    -e 's;DebugFocuser     Move: at \([0-9]*\).*targetPos: \([0-9]*\);DebugFocuser     Move: from \1 to \2;' 
#    -e 's;DebugFocuser     onTimer: stopped moving: at \([0-9]*\).*;DebugFocuser     Stopped at \1;' 
#    -e 's;^\(.*doShutdown: starting activity ShuttingDown.*\)$;\n\1;' 
#    -e 's;^\(.*DebugShutter     Stop: \)stopped\(.*\);\n\1done\2\n;' 
#    -e 's;^\(=====.*\)$;\n\1\n;'

